#!/bin/csh
##
## Purpose: get daily IGS ZTD-file from CDDIS
##
## Originally written by Feng Zhou on 03/11/2015 @ GFZ
##
## Email: zhouforme@163.com
##
## Section 1.1, GPS/Galileo technologies, GFZ German Research Centre for Geosciences
#
##===============================BEGIN PROGRAM=====================================##
if ($#argv < 2) then
more << EOF

   Usage $0 <yyyy> <doy> [ndays] [site_list]

   EXAMPLES: $0 2017 001
             $0 2017 001 10 /home/fzhou/work/site.list

EOF
   exit
endif

@ yyyy  = $1
set yy = `echo $yyyy | awk '{printf("%2.2d",$1-int($1/100)*100)}'`
@ doy   = $2
@ count = 1
if ($#argv >= 3) then
   @ count = $3
endif
set site_list = ''
if ($#argv >= 4) then
   set site_list = $4
endif

echo " "
echo " Downloading IGS ZTD files from ftp://cddis.gsfc.nasa.gov ..."
echo " "

while (${count})
   set cdoy = `echo $doy | awk '{printf("%3.3d\n",$1)}'`
   echo " Year:" ${yyyy} " DOY:" ${cdoy}
   
   set ztddir = /Users/zhoufeng/Documents/GNSS_download/igs_ztd/$yyyy$cdoy
   if (! -d ${ztddir}) /bin/mkdir -p ${ztddir}
   cd ${ztddir}
   
   if (${site_list} == '') then
      wget -qr -nH --cut-dirs=6 ftp://cddis.gsfc.nasa.gov/gps/products/troposphere/zpd/${yyyy}/${cdoy}
   else
      foreach site (`cat ${site_list}`)
         set site = `echo ${site} | tr "[A-Z]" "[a-z]"`
         wget -qr -nH --cut-dirs=6 ftp://cddis.gsfc.nasa.gov/gps/products/troposphere/zpd/${yyyy}/${cdoy}/${site}${cdoy}0.${yy}zpd.gz
      end
   endif

   sh_uncompress *zpd.gz

   @ count --
   @ doy ++
end
#
##================================END PROGRAM======================================##
