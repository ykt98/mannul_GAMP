#!/bin/csh
##
## Purpose: get monthly DCB files from CODE
##
## Originally written by Feng Zhou on 03/11/2015 @ GFZ
##
## Email: zhouforme@163.com
##
## Section 1.1, GPS/Galileo technologies, GFZ German Research Centre for Geosciences
#
##===============================BEGIN PROGRAM=====================================##
if ($#argv < 2) then
more << EOF

   Usage $0 <yyyy> <mm> [nmon]

   EXAMPLES: $0 2017 07
             $0 2017 07 2

EOF
   exit
endif

@ yyyy = $1
@ mm = $2
set yy = `echo $yyyy | awk '{printf("%s",substr($1,3,4))}'`
@ count = 1
if ($#argv == 3) then
   @ count = $3
endif

set dcbdir = /Users/zhoufeng/Documents/GNSS_download/dcb
if (! -d $dcbdir) mkdir -p $dcbdir
cd $dcbdir

echo " "
echo " Downloading CODE DCB files from ftp://ftp.aiub.unibe.ch ..."
echo " "

while ($count)
   set cmm = `echo $mm | awk '{printf("%2.2d",$1)}'`
   wget -qr -nH --cut-dirs=2 ftp://ftp.aiub.unibe.ch/CODE/$yyyy/P1C1$yy$cmm.DCB.Z
   wget -qr -nH --cut-dirs=2 ftp://ftp.aiub.unibe.ch/CODE/$yyyy/P1P2$yy$cmm.DCB.Z
   wget -qr -nH --cut-dirs=2 ftp://ftp.aiub.unibe.ch/CODE/$yyyy/P2C2$yy$cmm"_RINEX".DCB.Z
   
   sh_uncompress P1C1$yy$cmm.DCB.Z P1P2$yy$cmm.DCB.Z
   if (-e P2C2$yy$cmm"_RINEX".DCB.Z) then
      sh_uncompress P2C2$yy$cmm"_RINEX".DCB.Z
      /bin/mv P2C2$yy$cmm"_RINEX".DCB P2C2$yy$cmm.DCB
   endif
   
   @ count --
   @ mm ++
end
#
##================================END PROGRAM======================================##
