#!/bin/csh
##
## Purpose: get IGS precise satellite orbit and clock products from CDDIS
##
## Originally written by Feng Zhou on 17/11/2015 @ GFZ
##
## Email: zhouforme@163.com
##
## Section 1.1, GPS/Galileo technologies, GFZ German Research Centre for Geosciences
#
##===============================BEGIN PROGRAM=====================================##
if ($#argv < 2) then
more << EOF

   Usage $0 <yyyy> <doy> [ndays] [ac]

   EXAMPLES: $0 2017 001
             $0 2017 001 10 esa

EOF
   exit
endif

@ yyyy  = $1
@ doy   = $2
@ count = 1
if ($#argv >= 3) then
   @ count = $3
endif
set ac = 'igs'
if ($#argv >= 4) then
   set ac = $4
endif

set orbdir = /Users/zhoufeng/Documents/GNSS_download/igs_prod
if (! -d ${orbdir}) /bin/mkdir -p ${orbdir}
cd ${orbdir}

echo " "
echo " Downloading IGS products from ftp://cddis.gsfc.nasa.gov ..."
echo " "

while ($count)
   set cdoy = `echo ${doy} | awk '{printf("%3.3d\n",$1)}'`
   echo " Year:" ${yyyy} " DOY:" ${cdoy}
   
   set WEEKD = `mjday ${doy} ${yyyy} | awk '{nwk=int(($1-44244)/7);nwkd=$1-44244-nwk*7;print nwk*10+nwkd}'`
   set WEEK  = `echo ${WEEKD} | awk '{print substr($1,1,4)}'`

   if (${ac} == 'igs') then
      wget -qr -nH --cut-dirs=4 ftp://cddis.gsfc.nasa.gov/pub/gps/products/${WEEK}/igs${WEEKD}.sp3.Z
      wget -qr -nH --cut-dirs=4 ftp://cddis.gsfc.nasa.gov/pub/gps/products/${WEEK}/igs${WEEKD}.clk_30s.Z
      wget -qr -nH --cut-dirs=4 ftp://cddis.gsfc.nasa.gov/pub/gps/products/${WEEK}/igs${WEEKD}"7".erp.Z
      gunzip *.clk_30s.Z
   else if (${ac} == 'cod') then
      wget -qr -nH --cut-dirs=4 ftp://cddis.gsfc.nasa.gov/pub/gps/products/${WEEK}/cod${WEEKD}.eph.Z
      wget -qr -nH --cut-dirs=4 ftp://cddis.gsfc.nasa.gov/pub/gps/products/${WEEK}/cod${WEEKD}.clk.Z
      wget -qr -nH --cut-dirs=4 ftp://cddis.gsfc.nasa.gov/pub/gps/products/${WEEK}/cod${WEEKD}.clk_05s.Z
      gunzip *.clk_05s.Z
   else
      wget -qr -nH --cut-dirs=4 ftp://cddis.gsfc.nasa.gov/pub/gps/products/${WEEK}/${ac}${WEEKD}.sp3.Z
      wget -qr -nH --cut-dirs=4 ftp://cddis.gsfc.nasa.gov/pub/gps/products/${WEEK}/${ac}${WEEKD}.clk.Z
      wget -qr -nH --cut-dirs=4 ftp://cddis.gsfc.nasa.gov/pub/gps/products/${WEEK}/${ac}${WEEK}"7".erp.Z
   endif
   
   sh_uncompress *.Z

   @ count --
   @ doy ++
end
#
##================================END PROGRAM======================================##
